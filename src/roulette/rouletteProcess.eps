import roulette.roulettePhysics;
import roulette.rouletteString;
import bet.betRegions;
import bet.betProcess;
import utils.visualutils;
import utils.sounds;
import utils.utils;
import utils.loc;

var t = 0;

function startRoulette() {
    t = 0;
    roulettePhysics.rollRoulette();
}

function rouletteLoop() {
    roulettePhysics.rouletteLoop();

    for(var player = 0 ; player < 6 ; player++) {
        if(playerexist(player) && Bring(player, AtLeast, 1, "ScanPos", "betZone")) {
            SetCurrentPlayer(player);
            DisplayText("\x03[에러] \x04룰렛이 진행중일 때는 베팅할 수 없습니다..");
            PlayWAV("sound\\Misc\\PError.WAV");
        }
    }


    if(roulettePhysics.isRouletteFinished()) {
        const rouletteNum = roulettePhysics.getRouletteNumber();
        if(t == 0) {
            sounds.broadcastSound("staredit\\wav\\powerUp.ogg");
            utils.broadcastText(rouletteString.stringTable[rouletteNum]);

            // 승자 표시
            betRegions.loopBetRegions(function (rgn: betRegions.BetRegion) {
                const multiplier = rgn.betChecker(rouletteNum);
                if(multiplier) {
                    visualutils.fillRect(rgn.pos, "Winner", P7);
                }
            });
        }

        // 배당률만큼 퍼준다
        betProcess.payBackBets(rouletteNum);

        t++;
        if(t >= 5 * 24 && Bring(Force1, Exactly, 0, '(men)', 'betZone')) {
            KillUnit("Winner", P7);
            t = 0;
            SetResources(Force1, Add, 1000, Ore);
            utils.broadcastText("\x07 * \x04기본수당 1000만원이 입금되었습니다.");
            sounds.broadcastSound("staredit\\wav\\onIncome.ogg");
            return 0;
        }
    }
    else {
        // 베팅자금 묶어두자
        betRegions.loopBetRegions(function (rgn: betRegions.BetRegion) {
            const pos = rgn.pos;
            loc.setLocationPos($L("cloc1"), pos.x, pos.y, pos.w, pos.h);
            MoveUnit(All, "(any unit)", AllPlayers, "cloc1", "cloc1");
            Order("(any unit)", AllPlayers, "cloc1", Move, "cloc1");
        });
    }

    return 1;
}
