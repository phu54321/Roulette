import bet.betRegions;
import bet.betProcess;
import utils.sounds;
import utils.utils;
import roulette.rouletteProcess;
import chips;
import scan2pos;
import bgmplayer;

function onPluginStart() {
    sounds.loadSounds();
    randomize();

    for(var player = 0 ; player < 6 ; player++) {
        if(playerexist(player)) {
            SetCurrentPlayer(player);
            SetResources(player, SetTo, 10000, Ore);
            DisplayText("\x13\x11===============================\n\x13\x04집 값 룰 렛\n\n\x13\x1B5억을 벌어 아파트에 입주하세요!\n\x13\x16자세한 플레이방법은 F10+J\n\x13\x11===============================\n\n\x13도박상담은 국번없이 1366\n");
            SetMissionObjectives("\x03[목표] \x045억 벌기  \x01(미네랄 50000)\n\n\x08[[ \x04스캔으로 베팅금액 / 범위 설정 가능 \x08]]\n\x1D * \x04베팅금액: 위쪽의 \x1110, 100, 1000, 10000 \x04스캔\n\x1D * \x04베팅범위: 아래 \x10사이언스베슬/커세어 \x04스캔\n\n\x1E! 배당률: 36 / (해당하는 숫자 갯수) !");
        }
    }

    // Vision
    RunAIScript("Turn ON Shared Vision for Player 8");
    SetMemory(0x51CE98, SetTo, 2);  // Update vision instantly

    // Leaderboard
    LeaderBoardResources(Ore, "\x04현재 자본");
    LeaderBoardComputerPlayers(Disable);

    // Init betting locations
    betRegions.initBetRegions();
    betProcess.startBetting();
}


const STATE_BET = 0;
const STATE_ROULETTE = 1;
var state = STATE_BET;
var isGameOver = false;

function gameEndProcess();

function afterTriggerExec() {
    bgmplayer.bgmloop();

    if(isGameOver) return;
    scan2pos.translateScanToPosition();

    KillUnit("(any unit)", P12);
    SetInvincibility(Enable, '(any unit)', AllPlayers, 'Anywhere');

    betProcess.updateBetUnits();

    // State machine
    if (state == STATE_ROULETTE) {
        if(rouletteProcess.rouletteLoop() == 0) {
            betProcess.startBetting();
            state = STATE_BET;
        }
    }
    else if (state == STATE_BET) {
        if(betProcess.bettingLoop() == 0) {
            rouletteProcess.startRoulette();
            state = STATE_ROULETTE;
        }
    }

    // Various updates
    gameEndProcess();
    utils.updateMinimap();

    RemoveUnit("ScanPos", Force1);
}


function gameEndProcess() {
    const isVictoryPlayer = EUDArray(6);

    for(var player = 0 ; player < 6 ; player++) {
        if(!playerexist(player)) continue;
        SetCurrentPlayer(player);
        if(MostResources(Ore) && Accumulate(CurrentPlayer, AtLeast, 100000, Ore)) {
            DisplayText("\x13\x11※※※※※※※※※※※※※※※※※※※\n\n\x13\x04   10억을 벌어 내집마련에 성공했습니다.\n\n\x13\x11※※※※※※※※※※※※※※※※※※※\n");
            PlayWAV("sound\\Misc\\transmission.WAV");
            PlayWAV("staredit\\wav\\onVictory.ogg");
            isGameOver = true;
            isVictoryPlayer[player] = 1;
        }
    }

    if(isGameOver) {
        for(var player = 0 ; player < 6 ; player++) {
            if(!playerexist(player)) continue;
            SetCurrentPlayer(player);

            if(!isVictoryPlayer[player]) {
                DisplayText("\x06누군가 이미 10억을 벌었습니다....");
                PlayWAV("sound\\Misc\\transmission.WAV");
                Defeat();
            }
            else {
                for(var p2 = 0 ; p2 < 6 ; p2++) {
                    if(isVictoryPlayer[p2]) SetAllianceStatus(p2, AlliedVictory);
                }
            }
        }

        for(var player = 0 ; player < 6 ; player++) {
            if(!playerexist(player)) continue;
            SetCurrentPlayer(player);

            if(isVictoryPlayer[player]) {
                Victory();
            }
        }
    }
}
