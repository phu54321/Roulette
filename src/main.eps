import bet.betRegions;
import bet.betProcess;
import utils.sounds;
import utils.utils;
import roulette.rouletteProcess;
import chips;
import scan2pos;

function onPluginStart() {
    sounds.loadSounds();
    randomize();

    for(var player = 0 ; player < 6 ; player++) {
        if(playerexist(player)) {
            SetCurrentPlayer(player);
            SetResources(player, SetTo, 10000, Ore);
            DisplayText("\04초기자금 \x071억원\x04이 주어졌습니다.");
        }
    }

    // Vision
    RunAIScript("Turn ON Shared Vision for Player 8");
    SetMemory(0x51CE98, SetTo, 2);  // Update vision instantly

    // Leaderboard
    LeaderBoardResources(Ore, "\x04현재 자본");
    LeaderBoardComputerPlayers(Disable);

    // Init betting locations
    betRegions.initBetRegions();
    rouletteProcess.startRoulette();
}


const STATE_BET = 0;
const STATE_ROULETTE = 1;
var state = STATE_ROULETTE;

function afterTriggerExec() {
    scan2pos.translateScanToPosition();

    KillUnit("(any unit)", P12);
    SetInvincibility(Enable, '(any unit)', AllPlayers, 'Anywhere');

    betProcess.updateBetUnits();

    // State machine
    if (state == STATE_ROULETTE) {
        if(rouletteProcess.rouletteLoop() == 0) {
            betProcess.startBetting();
            state = STATE_BET;
        }
    }
    if (state == STATE_BET) {
        if(betProcess.bettingLoop() == 0) {
            rouletteProcess.startRoulette();
            state = STATE_ROULETTE;
        }
    }

    // Various updates
    chips.updateChipDisplay();
    utils.updateMinimap();
    // utils.clearRepulseMap();

    RemoveUnit("ScanPos", Force1);
}
